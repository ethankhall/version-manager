buildscript {
    ext {
        springBootVersion = '1.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/milestone'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'local-dev'
apply plugin: 'spring-boot'
apply from: "$rootDir/gradle/publishing.gradle"
apply plugin: 'docker'

artifactoryPublish.dependsOn assemble

dependencies {
    compile project(":api:rest-model")
    compile project(":database:database-api")
    compile project(":database:database-impl")
    compile project(":web-service:version-management")
    compile project(":web-service:api-project")
    compile project(":auth:authentication:default")
    compile project(":auth:authentication:core")

    //Caching
    compile libraries.springContextSupport
    compile 'net.sf.ehcache:ehcache:2.8.3'

    //Spring
    compile libraries.springBootWebapp

    //Hibernate
    compile libraries.hibernate

    //Misc
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile libraries.dropwizard

    testCompile project(":test-utils")
    testCompile libraries.groovy
    testCompile libraries.spock
    testCompile libraries.spockSpring

    testCompile libraries.springTest
    testCompile libraries.jsonPath

    testCompile libraries.testing
}

docker {
    maintainer 'Ethan Hall "ethan@ehdev.io"'
}

task buildDocker(type: Docker, dependsOn: assemble) {
    push = project.hasProperty("dockerPush")
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    tag = "conrad/${applicationName}"
    doFirst {
        copy {
            from jar
            into stageDir
            rename "${project.name}-.*.jar", 'webapp.jar'
        }
    }
}

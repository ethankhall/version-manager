buildscript {
    repositories {
        maven {
            url 'https://ehdev.artifactoryonline.com/ehdev/local-release/'
            credentials {
                username 'read'
                password 'WUKPJRpDY6AMqdGWxyce334QwCKj3KFw'
            }
        }
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'io.ehdev.conrad:gradle-plugin:+'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.2.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.0-beta-4589'
        classpath "org.flywaydb:flyway-gradle-plugin:3.2.1"
    }
}

allprojects {
    apply plugin: 'idea'
    group = 'io.ehdev.conrad'

    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/milestone'
        }
    }
}

apply from: "gradle/versions.gradle"
apply from: "gradle/database.gradle"
apply plugin: 'version-manager'
apply plugin: 'base'
apply plugin: 'jacoco'

versionManager {
    repoId = '3e79966b-a630-4efa-901b-8819c929c5e3'
    token = '08A7jgz3PYAcBFh9yZNY8PmsrnNxpEyHU4mgeTYFsntHDno6yRPMkvk7RuOR'
    providerBaseUrl = 'http://api.ehdev.io'
}


subprojects {
    buildDir = new File(rootProject.getProjectDir(), "build/${project.name}")
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component
                .find { it.@name == 'VcsDirectoryMappings' }
                .mapping.@vcs = 'Git'
    }
}

task testCoverage(type: JacocoMerge) {
    dependsOn { subprojects.collect { it.tasks.withType(Test) }.flatten() }
}
check.dependsOn testCoverage

gradle.taskGraph.afterTask { Task task, TaskState state ->
    if(task.extensions.findByType(JacocoTaskExtension)) {
        if(task.extensions.getByType(JacocoTaskExtension).destinationFile.exists()) {
            println "adding $task.name to testCoverage"
            testCoverage.executionData task
        }
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects.collect { it.tasks.withType(Test) }.flatten()
}

check.dependsOn testReport

task generateDocumentation(type: Copy) {
    from {
        subprojects.collect {
            it.tasks.withType(org.asciidoctor.gradle.AsciidoctorTask).collect { task -> task.outputDir }
        }.flatten()
    }
    into "$buildDir/documentation"
}

build.dependsOn generateDocumentation

task teamcityVersion {
    doLast {
        println "##teamcity[buildNumber '$version']"
    }
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'http://repo.ehdev.io/maven2'
        }
    }
    dependencies {
        classpath 'io.ehdev.conrad.client:gradle-client:+'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.3'
        classpath "org.flywaydb:flyway-gradle-plugin:4.0.3"
    }
}

allprojects {
    apply plugin: 'idea'
    group = 'io.ehdev.conrad'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://repo.spring.io/milestone'
        }
        maven {
            url "http://repository.jetbrains.com/all"
        }
    }

    apply from: "$rootDir/gradle/versions.gradle"

    afterEvaluate {
        configurations.all.each {
            it.resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.springframework') {
                    details.useVersion(ext.versions.spring)
                }
            }
        }
    }
}

subprojects {
    task allDeps(type: DependencyReportTask) {}
}

apply from: "gradle/database.gradle"
apply plugin: 'version-manager'
apply plugin: 'base'
apply plugin: 'jacoco'

versionManager {
    projectName = 'ethankhall'
    repoName = 'version-manager'
}


subprojects {
    buildDir = new File(rootProject.getProjectDir(), "build/${project.path.replace(':', '/').substring(1)}")
    apply plugin: 'java'

    task resolveDependencies {
        doLast {
            configurations.testRuntime.files
        }
    }

    tasks.withType(Test) { test ->
        test.maxHeapSize = '512m'
        test.environment 'SPRING_DATASOURCE_URL', database.url
        test.environment 'SPRING_DATASOURCE_USERNAME', database.user
        test.environment 'SPRING_DATASOURCE_PASSWORD', database.password
    }
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component
            .find { it.@name == 'VcsDirectoryMappings' }
            .mapping.@vcs = 'Git'
    }
}

task mergeReport(type: JacocoMerge) {
    dependsOn { subprojects.collect { it.tasks.withType(Test) }.flatten() }
    executionData fileTree(dir: "$rootDir/build", include: '**/*.exec')
}

evaluationDependsOnChildren()

task testCoverage(type: JacocoReport) {
    dependsOn mergeReport
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output).asFileTree.matching {
        exclude '**/io/ehdev/conrad/db/tables/**'
    }
    executionData = mergeReport.executionData

    onlyIf = {
        true
    }
}

check.dependsOn testCoverage

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects.collect { it.tasks.withType(Test) }.flatten()
}

check.dependsOn testReport

task generateDocumentation(type: Copy) {
    from {
        subprojects.collect {
            it.tasks.withType(org.asciidoctor.gradle.AsciidoctorTask).collect { task -> task.outputDir }
        }.flatten()
    }
    into "$buildDir/documentation"
}

build.dependsOn generateDocumentation

allprojects {
    apply plugin: 'idea'
    group = 'io.ehdev.conrad'
    version = '1.0.0'
}

subprojects {
    buildDir = new File(rootProject.getProjectDir(), "build/${project.name}")
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = '1.8'

    if(project.name != 'test-utils') {
        dependencies {
            testCompile project(':test-utils')
        }
    }

    test {
        useJUnit {
            excludeCategories 'io.ehdev.conrad.test.IntegrationTestGroup'
        }
    }

    def integTest = tasks.create('integTest', Test) {
        shouldRunAfter(tasks.test)
        reports.html.destination = file("$buildDir/reports/intTests")
        useJUnit {
            includeCategories 'io.ehdev.conrad.test.IntegrationTestGroup'
        }
    }

    tasks.withType(Test) {
        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }

    tasks.check.dependsOn(integTest)
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component
                .find { it.@name == 'VcsDirectoryMappings' }
                .mapping.@vcs = 'Git'
    }
}

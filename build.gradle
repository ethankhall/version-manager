plugins {
    id 'io.ehdev.gradle.dependency-script' version 'LATEST'
    id 'org.jetbrains.kotlin.jvm' version "1.2.51" apply false
    id 'org.jetbrains.kotlin.plugin.spring' version "1.2.51" apply false
    id 'io.ehdev.gradle.dependency-resolve' version 'LATEST' apply false
    id 'io.ehdev.gradle.build-dir' version 'LATEST' apply false
    id 'io.ehdev.gradle.klint' version 'LATEST' apply false
    id "org.flywaydb.flyway" version "5.1.4" apply false
    id 'base'
    id 'jacoco'
}

allprojects {
    apply plugin: 'idea'

    group = 'crom.tech'

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    apply from: "$rootDir/gradle/versions.gradle"
}

apply from: "gradle/database.gradle"

subprojects { sub ->
    apply plugin: 'java'
    apply plugin: 'io.ehdev.gradle.dependency-resolve'
    apply plugin: 'io.ehdev.gradle.build-dir'
    apply plugin: 'io.ehdev.gradle.dependency-script'

    tasks.create('allDeps', DependencyReportTask)

    tasks.withType(Test) { test ->
        test.maxHeapSize = '512m'
        test.environment 'SPRING_DATASOURCE_DBNAME', database.name
        test.environment 'SPRING_DATASOURCE_URL', database.url
        test.environment 'SPRING_DATASOURCE_USERNAME', database.user
        test.environment 'SPRING_DATASOURCE_PASSWORD', database.password
    }

    plugins.withType(org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapper) {
        sub.plugins.apply('io.ehdev.gradle.klint')
        sub.plugins.apply('org.jetbrains.kotlin.plugin.spring')
    }


    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    
    ext['libraries'] = deps.libraries
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component
            .find { it.@name == 'VcsDirectoryMappings' }
            .mapping.@vcs = 'Git'
    }
}

apply from: "gradle/coverage.gradle"

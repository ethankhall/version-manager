apply plugin: 'java'
apply plugin: 'local-dev'

sourceSets {
    main {
        java {
            srcDir 'src/main/generated-java'
        }
    }
}

configurations {
    migration
    codegen
    compile.extendsFrom codegen
}

dependencies {
    codegen libraries.database
    codegen libraries.jooqCodeGen

    migration project(':database:database-migration')

    compile project(':database:database-migration')
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'javax.persistence:persistence-api:1.0.2'
}

def createLibraryXml = task createLibraryXml {
    dependsOn ':database:database-migration:flywayMigrate'
    ext.libraryFile = file("$buildDir/library.xml")
    ext.outputDir = file('src/main/generated-java')
    inputs.files(configurations.migration)
    inputs.files(configurations.migration)
    outputs.dir(ext.outputDir)
    doFirst {
        (ext.libraryFile as File).parentFile.mkdirs()
    }
    doLast {
        def xml = new groovy.xml.StreamingMarkupBuilder().bind { bnd ->
            configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.7.0.xsd') {
                mkp.comment("Generated at ${new Date()}")
                jdbc() {
                    driver(database.driver)
                    url(database.url)
                    user(database.user)
                    password(database.password)
                }
                generator() {
                    database() {
                        bnd.name('org.jooq.util.mysql.MySQLDatabase')
                        excludes('schema_version')
                        inputSchema('version_manager_test')
                        customTypes() {
                            customType() {
                                bnd.name("Instant")
                                type('java.time.Instant')
                                converter('tech.crom.db.converter.TimestampConverter')
                            }
                        }
                        forcedTypes() {
                            forcedType() {
                                bnd.name('Instant')
                                types('timestamp')
                            }

                            forcedType() {
                                bnd.name('Boolean')
                                types('tinyint')
                            }
                        }
                    }
                    generate([:]) {
                        pojos true
                        daos false
                        jpaAnnotations true
                        validationAnnotations true
                    }
                    strategy() {
                        matchers() {
                            tables() {
                                table() {
                                    tableClass() {
                                        transform("PASCAL")
                                        expression('$0_Table')
                                    }
                                }
                            }
                        }
                    }
                    target() {
                        packageName('tech.crom.db')
                        directory(ext.outputDir.absolutePath)
                    }
                }
            }
        }

        ext.libraryFile.text = groovy.xml.XmlUtil.serialize(xml)
    }
}

task generateDataModel(type: JavaExec, dependsOn: ['createLibraryXml']) {
    inputs.files(configurations.codegen)
    inputs.file(createLibraryXml.libraryFile.absolutePath)
    outputs.dir(createLibraryXml.outputDir.absolutePath)
    setMain('org.jooq.util.GenerationTool')
    args(createLibraryXml.libraryFile.absolutePath)
    classpath configurations.codegen
}

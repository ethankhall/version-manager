plugins {
    id 'java'
    id 'groovy'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'local-dev'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/publishing.gradle"

repositories {
    mavenCentral()
}

configurations {
    swaggerCodeGen
    swaggerDeps
}

sourceCompatibility = '1.7'

dependencies {
    swaggerDeps project(path: ":web-service:api-project", configuration: 'swagger')
    swaggerCodeGen 'io.swagger:swagger-codegen-cli:2.1.5'

    compile project(":api:rest-model")
    compile ('org.eclipse.jgit:org.eclipse.jgit:4.1.0.201509280440-r') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    compile libraries.kotlin

    compile 'com.squareup.retrofit2:retrofit:2.0.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.0.0'
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.7.2"

    testCompile libraries.spock
    testCompile libraries.springTest
    testCompile libraries.jsonPath
    testCompile libraries.testing
    testCompile 'com.squareup.okhttp:mockwebserver:2.7.5'

    integTestCompile 'com.github.tomjankes:wiremock-groovy:0.2.0'
    integTestCompile 'com.github.tomakehurst:wiremock:1.58'
}

shadowJar {
    relocate 'com.fasterxml.jackson', 'jarjar.com.fasterxml.jackson'
}

task generateCode(type: JavaExec, dependsOn: [configurations.swaggerDeps]) {
    classpath = configurations.swaggerCodeGen
    main = 'io.swagger.codegen.SwaggerCodegen'
    args 'generate',
        '-l', 'java',
        '-i', configurations.swaggerDeps.singleFile.absolutePath,
        '-o', file('src/main/generated-java').absolutePath
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifact shadowJar
        }
    }
}

import io.ehdev.conrad.gradle.SpringRestDocPlugin

apply plugin: 'base'
apply plugin: 'org.asciidoctor.gradle.asciidoctor'
apply from: "$rootDir/gradle/publishing-base.gradle"

task mergeResults(type: Copy) {
    into "$buildDir/snippits"
}

evaluationDependsOn(':web-service:api-project')

afterEvaluate {
    rootProject.allprojects.each { proj ->
        if (proj.plugins.hasPlugin(SpringRestDocPlugin)) {
            def apiTest = proj.tasks.getByName('apiTest')
            mergeResults.from(apiTest.ext.snippetsDir)
            mergeResults.dependsOn apiTest
        }
    }
}

asciidoctor {
    attributes('snippets': mergeResults.destinationDir)
    inputs.dir mergeResults.destinationDir
    dependsOn mergeResults
    sourceDir 'src/main/asciidoc'
}

build.dependsOn asciidoctor

task zipped(type: Zip, dependsOn: 'asciidoctor') {
    from asciidoctor.outputDir.absolutePath + '/html5'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact zipped
        }
    }
}
